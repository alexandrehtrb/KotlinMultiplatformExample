apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "flavor"
    productFlavors {
        development {
            applicationIdSuffix ".development"
            resValue 'string', 'app_name', 'KotlinSharedAndroid-Dev'
        }
        homolog {
            applicationIdSuffix ".homolog"
            resValue 'string', 'app_name', 'KotlinSharedAndroid-Homolog'
        }
        mock {
            applicationIdSuffix ".mock"
            resValue 'string', 'app_name', 'KotlinSharedAndroid-Mock'
        }
        production {
            resValue 'string', 'app_name', 'KotlinSharedAndroid'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        final testCommonsDir = 'src/testCommons/java'
        test {
            java.srcDir testCommonsDir
            resources.srcDirs += fileTree(dir: "src/testCommons/resources")
        }
        androidTest {
            java.srcDir testCommonsDir
            resources.srcDirs += fileTree(dir: "src/testCommons/resources")
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':KotlinSharedLibrary')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$rootProject.androidSupportVersion"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support.test.espresso:espresso-idling-resource:$rootProject.espressoVersion"

    // for tests only
    final mockito = "org.mockito:mockito-core:$rootProject.mockitoVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation mockito
    testImplementation "junit:junit:$rootProject.jUnitVersion"

    androidTestImplementation mockito
    androidTestImplementation "com.android.support:support-annotations:$rootProject.androidSupportVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.testRunnerVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.testRunnerVersion"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$rootProject.uiAutomatorVersion"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$rootProject.dexMakerVersion"
    androidTestImplementation "com.linkedin.testbutler:test-butler-library:$rootProject.testButlerVersion"

    //kapt "com.android.databinding:compiler:$rootProject.databindingCompilerVersion"
}
