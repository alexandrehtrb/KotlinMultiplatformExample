apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion AppVersions.compileSdkVersion
    buildToolsVersion AppVersions.buildToolsVersion
    defaultConfig {
        applicationId AppVersions.applicationId
        minSdkVersion AppVersions.minSdkVersion
        targetSdkVersion AppVersions.targetSdkVersion
        versionCode AppVersions.versionCode
        versionName AppVersions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "flavor"
    productFlavors {
        development {
            applicationIdSuffix ".development"
            resValue 'string', 'app_name', 'KotlinSharedAndroid-Dev'
        }
        homolog {
            applicationIdSuffix ".homolog"
            resValue 'string', 'app_name', 'KotlinSharedAndroid-Homolog'
        }
        production {
            resValue 'string', 'app_name', 'KotlinSharedAndroid'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        final testCommonsDir = 'src/testCommons/java'
        test {
            java.srcDir testCommonsDir
            resources.srcDirs += fileTree(dir: "src/testCommons/resources")
        }
        androidTest {
            java.srcDir testCommonsDir
            resources.srcDirs += fileTree(dir: "src/testCommons/resources")
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':KotlinSharedLibrary')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$PluginVersions.kotlinVersion"
    implementation "com.android.support:appcompat-v7:$DependenciesVersions.androidSupportVersion"
    implementation "com.android.support:multidex:$DependenciesVersions.multidexVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$DependenciesVersions.espressoVersion"

    // for tests only
    final mockito = "org.mockito:mockito-core:$DependenciesVersions.mockitoVersion"

    implementation "com.android.support.constraint:constraint-layout:$DependenciesVersions.constraintLayoutVersion"
    testImplementation mockito
    testImplementation "junit:junit:$DependenciesVersions.jUnitVersion"

    androidTestImplementation mockito
    androidTestImplementation "com.android.support:support-annotations:$DependenciesVersions.androidSupportVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$DependenciesVersions.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$DependenciesVersions.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$DependenciesVersions.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$DependenciesVersions.espressoVersion"
    androidTestImplementation "com.android.support.test:rules:$DependenciesVersions.testRunnerVersion"
    androidTestImplementation "com.android.support.test:runner:$DependenciesVersions.testRunnerVersion"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$DependenciesVersions.uiAutomatorVersion"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$DependenciesVersions.dexMakerVersion"
    androidTestImplementation "com.linkedin.testbutler:test-butler-library:$DependenciesVersions.testButlerVersion"
}
