apply plugin: 'kotlin-multiplatform'
apply from: 'android-kotlin-shared.gradle'
apply from: 'jacoco.gradle'

final def MODULE_NAME = "KotlinSharedLibrary" // Nome do m√≥dulo

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework(MODULE_NAME)
            }
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain {
            dependencies {
                api KotlinDependencies.kotlinCommon
            }
        }

        commonTest {
            dependencies {
                implementation KotlinDependencies.kotlinTestCommon
                implementation KotlinDependencies.kotlinTestAnnotationsCommon
            }
        }

        androidMain {
            dependencies {
                api KotlinDependencies.kotlinStdlib
                api KotlinDependencies.kotlinAndroidExtensionsRuntime
            }
        }

        androidTest {
            dependencies {
                implementation AndroidDependencies.mockito
                implementation AndroidDependencies.jUnit
            }
        }

    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.ios.binaries.getFramework(MODULE_NAME, mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode